Value POWER_SUPPLY (\d+)
Value POWER_SUPPLY_MODEL (\S+)
Value POWER_SUPPLY_OUTPUT (\d+)
Value POWER_SUPPLY_INPUT (\d+)
Value POWER_SUPPLY_CAPACITY (\d+)
Value POWER_SUPPLY_STATUS (\w+)
Value Filldown POWER_SUPPLY_VOLTAGE (\d+)
Value FAN (\S+)
Value FAN_STATUS (\S+)
Value TEMPERATURE_MODULE (\d+)
Value TEMPERATURE_SENSOR (\S+)
Value TEMPERATURE_MAJOR_THRESH (\d+)
Value TEMPERATURE_MINOR_THRESH (\d+)
Value TEMPERATURE_CURRENT (\d+)
Value TEMPERATURE_STATUS (\S+)

Start
  ^Power Supply:\s*$$ -> Power
  ^Fan:\s*$$ -> Fan
  #^Fan\s+Model\s+Hw\s+(Direction\s+)?Status -> Fan

Power
  ^Power\s+Actual\s+Total -> Power
  ^Power\s+Actual\s+Actual\s+Total -> Power
  ^Voltage:\s+${POWER_SUPPLY_VOLTAGE}\s+Volts
  ^${POWER_SUPPLY}\s+${POWER_SUPPLY_MODEL}\s+${POWER_SUPPLY_OUTPUT}\s+\w+\s+${POWER_SUPPLY_CAPACITY}\s+\w+\s+${POWER_SUPPLY_STATUS} -> Record
  ^${POWER_SUPPLY}\s+${POWER_SUPPLY_MODEL}\s+${POWER_SUPPLY_OUTPUT}\s+\w+\s+${POWER_SUPPLY_INPUT}\s+\w+\s+${POWER_SUPPLY_CAPACITY}\s+\w+\s+${POWER_SUPPLY_STATUS} -> Record
  ^Module\s+Model\s+Draw\s+Allocated\s+Status\s* -> Start
  ^Power\s+Usage\s+Summary: -> Start
  ^\s*$$ -> Start
  ^Supply\s+Model\s+Output\s+Capacity\s+Status
  ^Supply\s+Model\s+Output\s+Input\s+Capacity\s+Status
  ^\s+\(Watts\s\)\s+
  ^-+\s+-+
  ^. -> Error

Fan
  ^Fan\s+Zone\s+Speed -> Temperature
  ^${FAN}\s+\S+\s+\S+\s+${FAN_STATUS}\s*$$ -> Record
  ^${FAN}\s+\S+\s+\S+\s+\S+\s+${FAN_STATUS}\s*$$ -> Record
  ^\s*$$ -> Start
  ^-+\s*$$
  ^. -> Error

Temperature
  ^${TEMPERATURE_MODULE}\s+${TEMPERATURE_SENSOR}(\s+)?\(\S+\)\s+${TEMPERATURE_MAJOR_THRESH}\s+${TEMPERATURE_MINOR_THRESH}\s+${TEMPERATURE_CURRENT}\s+${TEMPERATURE_STATUS} -> Record
